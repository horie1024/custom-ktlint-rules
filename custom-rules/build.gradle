plugins {
    id 'kotlin'
    id 'maven-publish'
}

task sourceJar(type: Jar) {
    from sourceSets.main.java.srcDirs
    archiveClassifier.set("sources")
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId "com.horie1024"
            artifactId "custom-ktlint-rules"
            version "$VERSION"
            artifact sourceJar
            artifact "$buildDir/libs/custom-rules.jar"

            // include any transitive dependencies
            pom {
                withXml {
                    def dependenciesNode = asNode().appendNode('dependencies')

                    project.configurations.implementation.allDependencies.each {
                        if (it.group != null || it.name != null || it.version != null || it.name == "unspecified") return

                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                    }
                }
            }
        }
    }
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/horie1024/custom-ktlint-rules")
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("USERNAME")
                password = project.findProperty("gpr.token") ?: System.getenv("TOKEN")
            }
        }
    }
}

dependencies {

    compileOnly 'com.pinterest.ktlint:ktlint-core:0.42.0'

    testImplementation "junit:junit:4.13.2"
    testImplementation "com.google.truth:truth:1.1.3"
    testImplementation 'com.pinterest.ktlint:ktlint-core:0.42.0'
    testImplementation 'com.pinterest.ktlint:ktlint-test:0.42.0'
}
